Class TommyGun : BloodWeapon replaces Chaingun
{	
	Default
	{
		Tag "Tommy gun";
		Inventory.PickupMessage "Picked up a Tommy gun.";
		Obituary "%o was shredded by %k's Tommy gun.";
		Weapon.SlotNumber 4;
		Weapon.SelectionOrder 3;
		Weapon.AmmoType "Bullets";
		Weapon.AmmoType2 "Bullets";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 50;
		Weapon.AmmoUse2 1;
		//Weapon.SisterWeapon "AkTommy";
		Scale 0.7;
	} int refire;
	States
	{
	Spawn: TOMM A 0; Goto SpawnLoop;
	Select: TOMG A 0; Goto SelectLoop;
	Deselect: TOMG A 0; Goto DeselectLoop;
	Ready: TOMG A 1 A_WeaponReady2(); Loop;
	Fire:
		TOMG B 1
		{
			A_TommyFire();
			A_TakeInventory("Bullets",1,TIF_NOTAKEINFINITE);
			
			A_Overlay(-11, "MuzzleFlash"); 
			A_Overlay(50, "Glow1");
			A_Overlay(51, "Glow");
			A_GunFlash("Flash");
			
			A_FireProjectile("CasingSpawner1",0,0,6,0);
			A_SpawnItemEx("LightTommy",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER,0); 
			A_WeaponReady2(WRF_DISABLE, 4, 4);
		}
		TOMG C 1 A_WeaponReady2(WRF_DISABLE,2,2);
		TOMG A 0 A_JumpIf(invoker.refire >= 1, 2);
		TOMG A 1 A_WeaponReady2(WRF_DISABLE,1,1);
		TOMG A 1 
		{ 
			invoker.refire++; 
			if(invoker.refire >= 2) invoker.refire = 0; 
			A_WeaponReady2(WRF_NOSWITCH,1,1); 
			A_ReFire("Fire"); 
		}
		Goto Ready;
	AltFire:
		TOMG B 0
		{
			If(CountInv("Bullets") > 0 && CountInv("Bullets") < 14) { Return ResolveState("Fire"); }
			If(CountInv("Bullets") == 0) { Return ResolveState("Reselect"); }
			Return ResolveState(Null);
		}
		TOMG A 0 A_JumpIf(!CountInv("Bullets"), 2);
		TOMG B 1 { A_TommyFireAlt(); A_Overlay(50, "Glow1"); A_Overlay(51, "Glow"); }
		TOMG C 1 A_WeaponReady2(WRF_DISABLE,2,2);
		TOMG A 0 A_JumpIf(!CountInv("Bullets"), 2);
		TOMG B 1 { A_TommyFireAlt(-4); A_Overlay(50, "Glow1"); A_Overlay(51, "Glow"); }
		TOMG C 1 A_WeaponReady2(WRF_DISABLE,-6,2);
		TOMG A 0 A_JumpIf(!CountInv("Bullets"), 2);
		TOMG D 1 { A_TommyFireAlt(-8); A_Overlay(50, "Glow2"); A_Overlay(51, "Glow"); }
		TOMG E 1 A_WeaponReady2(WRF_DISABLE,-10,2);
		TOMG A 0 A_JumpIf(!CountInv("Bullets"), 2);
		TOMG D 1 { A_TommyFireAlt(-12); A_Overlay(50, "Glow2"); A_Overlay(51, "Glow"); }
		TOMG E 1 A_WeaponReady2(WRF_DISABLE,-14,2);
		TOMG A 0 A_JumpIf(!CountInv("Bullets"), 2);
		TOMG D 1 { A_TommyFireAlt(-16); A_Overlay(50, "Glow2"); A_Overlay(51, "Glow"); }
		TOMG E 1 A_WeaponReady2(WRF_DISABLE,-18,2);
		TOMG A 0 A_JumpIf(!CountInv("Bullets"), 2);
		TOMG D 1 { A_TommyFireAlt(-20); A_Overlay(50, "Glow2"); A_Overlay(51, "Glow"); }
		TOMG E 1 A_WeaponReady2(WRF_DISABLE,-22,2);
		TOMG A 0 A_JumpIf(!CountInv("Bullets"), 2);
		TOMG D 1 { A_TommyFireAlt(-12); A_Overlay(50, "Glow2"); A_Overlay(51, "Glow"); }
		TOMG E 1 A_WeaponReady2(WRF_DISABLE,-10,2);
		TOMG A 0 A_JumpIf(!CountInv("Bullets"), 2);
		TOMG B 1 { A_TommyFireAlt(-2); A_Overlay(50, "Glow3"); A_Overlay(51, "Glow"); }
		TOMG C 1 A_WeaponReady2(WRF_DISABLE,0,2);
		TOMG A 0 A_JumpIf(!CountInv("Bullets"), 2);
		TOMG F 1 { A_TommyFireAlt(6); A_Overlay(50, "Glow4"); A_Overlay(51, "Glow"); }
		TOMG G 1 A_WeaponReady2(WRF_DISABLE,8,2);
		TOMG A 0 A_JumpIf(!CountInv("Bullets"), 2);
		TOMG F 1 { A_TommyFireAlt(12); A_Overlay(50, "Glow4"); A_Overlay(51, "Glow"); }
		TOMG G 1 A_WeaponReady2(WRF_DISABLE,14,2);
		TOMG A 0 A_JumpIf(!CountInv("Bullets"), 2);
		TOMG F 1 { A_TommyFireAlt(18); A_Overlay(50, "Glow4"); A_Overlay(51, "Glow"); }
		TOMG G 1 A_WeaponReady2(WRF_DISABLE,20,2);
		TOMG A 0 A_JumpIf(!CountInv("Bullets"), 2);
		TOMG F 1 { A_TommyFireAlt(18); A_Overlay(50, "Glow4"); A_Overlay(51, "Glow"); }
		TOMG G 1 A_WeaponReady2(WRF_DISABLE,16,2);
		TOMG A 0 A_JumpIf(!CountInv("Bullets"), 2);
		TOMG F 1 { A_TommyFireAlt(12); A_Overlay(50, "Glow4"); A_Overlay(51, "Glow"); }
		TOMG G 1 A_WeaponReady2(WRF_DISABLE,10,2);
		TOMG A 0 A_JumpIf(!CountInv("Bullets"), 2);
		TOMG F 1 { A_TommyFireAlt(6); A_Overlay(50, "Glow4"); A_Overlay(51, "Glow"); }
		TOMG G 1 A_WeaponReady2(WRF_DISABLE,4,2);
		TOMG A 0
		{
			A_ReFire();
			A_WeaponReady2(WRF_NOSWITCH);
			If(PressingFire()) { Return ResolveState("Fire"); }
			Return ResolveState(Null);
		}
		Goto Ready;
		
	MuzzleFlash:
		TNT1 A 0 A_Jump(256,1,2,3,4);
		TOMF A 0 A_Jump(256,4);
		TOMF B 0 A_Jump(256,3);
		TOMF C 0 A_Jump(256,2);
		TOMF D 0;
		"####" "#" 1 BRIGHT
		{
			A_OverlayFlags(-11,PSPF_ADDWEAPON|PSPF_ADDBOB,1);
		}
		Stop;
	Glow1: TOMG BCAAAAAAA 1 BRIGHT; Stop;
	Glow2: TOMG DEE 1 BRIGHT; Stop;
	Glow3: TOMG BCA 1 BRIGHT; Stop;
	Glow4: TOMG FGAAAAAAA 1 BRIGHT; Stop;
	Glow:
		TNT1 A 1
		{
			A_OverlayFlags(50, PSPF_RENDERSTYLE|PSPF_ALPHA, 1);
			A_OverlayRenderstyle(50, STYLE_Translucent);
			A_OverlayAlpha(50, 1.0);
		}
		TNT1 A 1 A_OverlayAlpha(50, 0.75);
		TNT1 A 1 A_OverlayAlpha(50, 0.5);
		TNT1 A 1 A_OverlayAlpha(50, 0.25);
		TNT1 A 1 A_OverlayAlpha(50, 0.15);
		TNT1 A 1 A_OverlayAlpha(50, 0.1);
		TNT1 A 1 A_OverlayAlpha(50, 0.075);
		TNT1 A 1 A_OverlayAlpha(50, 0.05);
		TNT1 A 1 A_OverlayAlpha(50, 0.025);
		Stop;
	
	Flash:
		TNT1 A 1 A_Light(2);
		TNT1 A 1 A_Light(1);
		Goto LightDone;
	Recoil:
		TNT1 A 0 A_Overlay(666,"SideRecoil");
		TNT1 A 0 A_Jump(128,4);
		TNT1 A 0 A_SetPitch(Pitch - 0.5, SPF_INTERPOLATE);
		TNT1 AA 1 A_SetPitch(Pitch + 0.25, SPF_INTERPOLATE);
		Stop;
		TNT1 A 0 A_SetPitch(Pitch + 0.5, SPF_INTERPOLATE);
		TNT1 AA 1 A_SetPitch(Pitch - 0.25, SPF_INTERPOLATE);
		Stop;
	SideRecoil:
		TNT1 A 0 A_Jump(128,4);
		TNT1 A 0 A_SetAngle(Angle - 0.5, SPF_INTERPOLATE);
		TNT1 AA 1 A_SetAngle(Angle + 0.25, SPF_INTERPOLATE);
		Stop;
		TNT1 A 0 A_SetAngle(Angle + 0.5, SPF_INTERPOLATE);
		TNT1 AA 1 A_SetAngle(Angle - 0.25, SPF_INTERPOLATE);
		Stop;
	}
	
	action void A_TommyFire()
	{
		A_QuakeEx(1,1,1,4,0,1,"",QF_RELATIVE|QF_SCALEDOWN,rollIntensity:0.5);
		A_StartSound("Tommygun", CHAN_WEAPON, CHANF_OVERLAP);
		A_AlertMonsters();
		A_FireWeapon(random[Blood](4,6), 1, 0.033, 'Normal', -4);
	}
	
	action void A_TommyFireAlt(double x = 0)
	{
		A_QuakeEx(1,1,1,4,0,1,"",QF_RELATIVE|QF_SCALEDOWN,rollIntensity:0.5);
		A_StartSound("Tommygun", CHAN_WEAPON, CHANF_OVERLAP);
		A_AlertMonsters();
		A_FireWeapon(random[Blood](4,6), 1, 0.067, 'Normal', -4, x * 0.55);
			A_Overlay(-11, "MuzzleFlash"); A_WeaponReady2(WRF_DISABLE,4+x,4);
		
		A_TakeInventory("Bullets",1,TIF_NOTAKEINFINITE);
		A_GunFlash("Flash");
		A_FireProjectile("CasingSpawner1",0,0,6+(x*0.15),0);
		A_SpawnItemEx("LightTommy",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER,0); 
	}
}

Class TommyBullet : Nothing
{
	Default
	{
		DamageFunction (random(4,6));
		Speed 156;
	}
}

Class Bullets : Ammo replaces Clip
{
	Default
	{
		Scale .65;
		Inventory.PickupMessage "Picked up a few bullets.";
		inventory.pickupsound "GotAmmo";
		Inventory.Amount 15;
		Inventory.MaxAmount 500;
		Ammo.BackpackAmount 50;
		Ammo.BackpackMaxAmount 750;
		Inventory.Icon "BULLA0";
		+DONTSPLASH
	}
	States
	{
	Spawn:
		BULL A -1;
		Loop;
	}
}

Class LightTommy : DynamicLight
{
	int cnt;
	Default
	{
		DynamicLight.Type "Point";
		args 255,224,128,60;
	}
	override void Tick()
	{
		Super.Tick();
		if ( !master ) { Destroy(); return; }
		if ( master.player ) SetOrigin(master.Vec2OffsetZ(0,0,master.player.viewz),true);
		else SetOrigin(master.pos,true);
		args[LIGHT_INTENSITY] -= 3;
		If(cnt > 2)
		{
			args[LIGHT_RED] *= 0.67;
			args[LIGHT_GREEN] *= 0.67;
			args[LIGHT_BLUE] *= 0.67;
		}
		if ( cnt++ > 9 ) Destroy();
	}
}