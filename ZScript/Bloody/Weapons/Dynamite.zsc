Class BloodyDynamite : BloodyThrowable
{	
	Default
	{
		Tag "TNT Bundle";
		Inventory.PickupMessage "Picked up a TNT Bundle.";
		Obituary "%o caught %k's TNT Bundle.";
		Weapon.SlotNumber 6;
		Weapon.SelectionOrder 3;
		Weapon.AmmoType "DynamiteAmmo";
		Weapon.AmmoType2 "DynamiteAmmo";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 1;
		Weapon.AmmoUse2 1;
		-WEAPON.CHEATNOTWEAPON
		Scale 0.8;
	}
	
	/*Override void DoEffect()
	{
		Super.DoEffect();
		If(!Owner || !Owner.Player || !(Owner.Player.ReadyWeapon == Self)) Return;
		
		If(CheckWater()) //&& !Holstered)
		{
			A_SetInventory("ThrowableForce", 0);
			If(Timer)
			{
				ThrowDynamite("DynamiteProj", true);
				Timer = 0;
			}
			SetStateLabel("Holster");
			//Holstered = True;
		}
	}*/
	
	Action State DynamiteReady(int flags = 0, double x = 0, double y = 0, bool canlower = true)
	{
		A_WeaponReady2(flags, x, y);
		If(CanLower && (Invoker.Owner.Health <= 0 && (Flags & WRF_NOSWITCH) != WRF_NOSWITCH)) Return A_Jump(256, "Deselect"); 
		If(CheckWater() && CanLower) 
		{
			A_GiveInventory("DynamiteHolster", 1);
			A_SetInventory("ThrowableForce", 0);
			ClearOverlay(RightFX1); 
			ClearOverlay(RightGlow1);
			A_StopSound(CHAN_WEAPON);
			Overlay(Right, "Dynamite_Lower");
			Return A_Jump(256, "Holster");
		}
		Return ResolveState(Null);
	}
	
	States
	{
	Spawn: TNTP F 0; Goto SpawnLoop;
	Select: TNTB A 0 { If(CheckWater()) Return ResolveState("SelectWater"); Else Overlay(Left, "Lighter_Raise"); Return ResolveState(Null); } Goto SelectLoop;
	Deselect:
		TNT1 A 0 A_JumpIf(CountInv("DynamiteHolster") == 1, "HolsterDeselect");
		TNTB A 0 { If(!CheckWater()) Overlay(Left, "Lighter_Lower"); } 
		Goto DeselectLoop;
	Ready:
		TNT1 A 0 A_JumpIf(CountInv("DynamiteHolster") == 1, "Holster");
		TNTB A 1 DynamiteReady();
		Loop;
	Fire:
		TNTB A 0 Overlay(Left, "Lighter_Move_Up");
		TNTB A 1 DynamiteReady(WRF_DISABLE, -2, -1);
		TNTB A 1 DynamiteReady(WRF_DISABLE, -4, -2);
		TNTB A 1 DynamiteReady(WRF_DISABLE, -8, -4);
		TNTB A 1 DynamiteReady(WRF_DISABLE, -12, -6);
		TNTB A 1 DynamiteReady(WRF_DISABLE, -14, -7);
		TNTB A 1 DynamiteReady(WRF_DISABLE, -16, -8);
		TNTB A 1 DynamiteReady(WRF_DISABLE, -17, -8.5);
		TNTB A 1 DynamiteReady(WRF_DISABLE, -18, -9);
		TNTB A 1 DynamiteReady(WRF_DISABLE, -19, -9.5);
	LightTheFuse:
		TNTB A 0 { Overlay(RightFX1, "FuseSparks"); Invoker.SparkCycles = 0; Invoker.SparkY = 0; A_StartSound("TNTFuse", CHAN_WEAPON, CHANF_OVERLAP|CHANF_LOOP, 1.0, 1.25); }
		TNTB AA 1 {DynamiteReady(WRF_DISABLE, -20, -10, false);	ThrowableCharge(); }
		TNTB A 1 { DynamiteReady(WRF_DISABLE, -18, -8, false);	ThrowableCharge(); }
		TNTB A 1 { DynamiteReady(WRF_DISABLE, -12, -2, false);	ThrowableCharge(); }
		TNTB A 1 { DynamiteReady(WRF_DISABLE, -4, 6, false);	ThrowableCharge(); }
		TNTB A 1 { DynamiteReady(WRF_DISABLE, 2, 16, false);	ThrowableCharge(); }
		TNTB A 1 { DynamiteReady(WRF_DISABLE, 8, 24, false);	ThrowableCharge(); }
		TNTB A 1 { DynamiteReady(WRF_DISABLE, 10, 30, false); 	ThrowableCharge(); }
		TNTB A 1 { DynamiteReady(WRF_DISABLE, 12, 34, false); 	ThrowableCharge(); }
		TNTB A 1 { DynamiteReady(WRF_DISABLE, 13, 36, false); 	ThrowableCharge(); }
		TNTB A 1 { DynamiteReady(WRF_DISABLE, 13.5, 38, false); ThrowableCharge(); }
	Hold:
		TNTB A 0 A_JumpIf(Invoker.Timer >= 175, "ReleaseTheButton");
		TNTB A 1 
		{ 
			DynamiteReady(WRF_DISABLE, 14, 40, false); 	
			ThrowableCharge();
		}
		TNTB A 0 
		{
			If(CheckWater()) Return ResolveState("Release");
			If(Invoker.Owner.Health > 0) A_ReFire("Hold");
			Else Return ResolveState("Hold");
			Return ResolveState(Null);
		}
		Goto Release;
	Release:
		TNTB A 1 DynamiteReady(WRF_DISABLE, 14.25, 56, false);
		TNTB A 1 DynamiteReady(WRF_DISABLE, 14.5, 72, false);
		TNTD A 0
		{
			If(!CheckWater())
			{
				A_GunFlash();
				Return ResolveState("Throw");
			}
			
			Else
			{
				A_SetInventory("ThrowableForce", 0);
				ClearOverlay(RightFX1); 
				ClearOverlay(RightGlow1);
				ThrowDynamite("DynamiteProj", True);
				A_StopSound(CHAN_WEAPON);
				Return ResolveState("Holster");
			}
			Return ResolveState(Null);
		}
		Goto Holster;
	Flash:
		TNT1 A 4 { ClearOverlay(RightFX1); ClearOverlay(RightGlow1); }
		TNTD A 0 { ThrowDynamite("DynamiteProj"); A_TakeInventory("DynamiteAmmo", 1, TIF_NOTAKEINFINITE); A_StopSound(CHAN_WEAPON); }
		Stop;
	BringBack:
		TNTB A 0 Overlay(Left, "Lighter_Move_Reset");
		Goto BringBack_Raise;
	
	FuseSparks:
		TNTB BBB 1 BRIGHT OverlayOffset(RightFX1, (.67*Invoker.SparkCycles, Invoker.SparkY));
		TNTB CCC 1 BRIGHT OverlayOffset(RightFX1, (.67*Invoker.SparkCycles, Invoker.SparkY));
		TNT1 A 0
		{
			If(Invoker.SparkCycles == 13) Invoker.SparkY += 1;
			If(Invoker.SparkCycles == 21) Invoker.SparkY += 1;
			If(Invoker.SparkCycles++ >= 25) Return ResolveState("FuseSparksEnd");
			Return ResolveState(Null);
		}
		Loop;
	FuseSparksEnd:
		TNTB DDD 1 BRIGHT OverlayOffset(RightFX1, (0, 0), False); // 54
		TNTB EEE 1 BRIGHT OverlayOffset(RightFX1, (0, 0)); // 55
		TNTB FFF 1 BRIGHT OverlayOffset(RightFX1, (0, 0)); // 56
		TNTB GGG 1 BRIGHT OverlayOffset(RightFX1, (0, 0)); // 57
		TNTB HHH 1 BRIGHT OverlayOffset(RightFX1, (0, 0)); // 58
		TNTB III 1 BRIGHT OverlayOffset(RightFX1, (0, 0)); // 58
		Stop;
	FuseGlow:
		TNTB A 1 BRIGHT;
		Stop;
	Dynamite_Lower:
		TNTB A 1 OverlayOffset(Right, (3, 3));
		TNTB A 1 OverlayOffset(Right, (9, 12));
		TNTB A 1 OverlayOffset(Right, (15, 20));
		TNTB A 1 OverlayOffset(Right, (21, 28));
		TNTB A 1 OverlayOffset(Right, (27, 36));
		TNTB A 1 OverlayOffset(Right, (39, 48));
		Stop;
	}
	
	int SparkCycles;
	double SparkY;
}

Class DynamiteAmmo : Ammo
{
	Default
	{
		Inventory.Amount 1;
		Inventory.MaxAmount 50;
		Ammo.BackpackAmount 5;
		Ammo.BackpackMaxAmount 100;
	}
}

Class DynamiteBox : DynamiteAmmo
{
	Default
	{
		Radius 12;
		Height 12;
		Inventory.PickupSound "GotAmmo";
		Inventory.PickupMessage "Picked up a box of TNT.";
		Inventory.Amount 5;
		Scale 0.575;
	}
	
	bool RotDir;
	Override void PostBeginPlay()
	{
		Angle = frandom[Blood](0, 359);
		RotDir = random[Blood](0, 1);
	}
	
	States
	{
	Spawn:
		TNTP B 1 NoDelay A_Turn(bDROPPED ? Invoker.RotDir ? -4. : 4. : 0);
		Loop;
	}
}

Class DynamiteProj : DynamiteBase
{
	Override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		Timer = Max(0, (175 - Stamina));
		A_StartSound("TNTFuse", CHAN_WEAPON, CHANF_OVERLAP|CHANF_LOOP, 1.0, 0.95);
	}
	
	Override void Tick()
	{
		super.Tick();
		If(IsFrozen()) Return; 
		DynamiteTrail("Yellow");
		If(Timer-- <= 0) 
		{ 
			SetStateLabel("Explode"); 
			Timer = 0x7FFFFFFF;
			Vel = (0,0,0);
			bTHRUACTORS = TRUE;
		}
	}
	
	Action Void DynamiteBounce()
	{
		If((Tracer && !(Tracer is 'RatburgerBloodChunk1') && !(Tracer is 'RatburgerBloodChunk1b') && !(Tracer is 'RatburgerCorpseHitbox')) || !bAMBUSH)
			Invoker.Timer = 0;
		
		Else If(Invoker.Timer > 0) A_StartSound("TNTBounce", CHAN_BODY, CHANF_OVERLAP, 1.0);
	}
	
	States
	{
	Bounce.Actor:
		TNT1 A 0 DynamiteBounce();
	Bounce:
		TNT1 A 0 DynamiteBounce();
	Spawn:
		TNT1 A 0 NoDelay A_Jump(8, 1, 2, 3, 4, 5, 6, 7, 8);
		Loop;
	See:
		TNTX DEFGHIJK 4 BRIGHT;
		Loop;
	Death:
		TNTX A 0 DynamiteBounce();
		TNTY A 0 A_ChangeVelocity(invoker.prevspeed.x * 1.25, invoker.prevspeed.y * 1.25, 0, 0);
		TNTX ABC 4 BRIGHT
		{
			If(Self.Pos.Z <= FloorZ) Vel *= 0.99;
			If(Invoker.Timer-- <= 0) Return ResolveState("Explode");
			Return ResolveState(Null);
		}
		Goto Death+1;
	}
}