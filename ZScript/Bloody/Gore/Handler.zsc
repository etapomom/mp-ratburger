Class RatburgerBloodHandler : EventHandler
{
	Override void CheckReplacement(ReplaceEvent e)
	{
		Super.CheckReplacement(e);
		If(	Ratburg_Gore_Toggle && 
			((e.Replacee == 'Blood') || 
			(e.Replacee == 'BloodSplatter') || 
			(e.Replacee == 'AxeBlood'))) 
				e.Replacement = 'RatburgerBlood';
	}
	
	Override void WorldThingDied(WorldEvent e)
	{
		If(!e.Thing) Return;
		//If(e.Thing is 'RatburgerBloodChunk1' || e.Thing is 'RatburgerBloodChunk1b' || e.Thing is 'RatburgerCorpseHitbox') Return;
		
		If(	e.Thing && !e.Thing.bNOBLOOD && 
			!(e.Thing is 'RatburgerBloodChunk1') && !(e.Thing is 'RatburgerBloodChunk1b') && !(e.Thing is 'RatburgerCorpseHitbox') &&
			((e.Inflictor && e.Inflictor.bEXTREMEDEATH) || (e.Thing.Health < e.Thing.GetGibHealth())) && (!e.Inflictor || !e.Inflictor.bNOEXTREMEDEATH))
			{
				Actor b = e.Thing.Spawn("RatburgerGibber", e.Thing.pos);
				If(b)
				{
					b.SetShade(e.Thing.BloodColor);
					b.CopyBloodColor(e.Thing);
					b.Master = e.Thing;
					b.Target = e.Thing;
					b.Vel = e.Thing.Vel;
				}
			}
		
		If(e.Thing && !e.Thing.Player)
		{
			Actor a = e.Thing.Spawn("RatburgerCorpseHitbox", e.Thing.pos);
			If(a)
			{
				a.SetShade(e.Thing.BloodColor);
				a.CopyBloodcolor(e.Thing);
				a.Master = e.Thing;
				a.Target = e.Thing;
				a.Vel = e.Thing.Vel;
			}
		}
	}
}