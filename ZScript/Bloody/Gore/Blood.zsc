Class RatburgerBlood : Blood
{
	Default
	{
		+NOBLOCKMAP
		+PUFFGETSOWNER
		+FORCEXYBILLBOARD
		+RANDOMIZE
		+DONTSPLASH
		+NOTELEPORT
		+NOGRAVITY
		+ROLLSPRITE
		+ROLLCENTER
		+RIPPER
		+BLOODLESSIMPACT
		+PAINLESS
		+THRUACTORS
		-RANDOMIZE
		-SOLID
		-ALLOWPARTICLES
		Scale 0.5;
		RenderStyle "Translucent";
	}
	
	const BLOODY = SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION;
	
	Override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		A_SetRoll(frandom(-20,20));
		bSPRITEFLIP = random(0,1);
		Scale.X *= frandom(0.95,1.05);
		Scale.Y = Scale.X;
		
		Actor a = Spawn("RatburgerBloodTrailer", Self.Pos);
		If(a)
		{
			If(Random[Blood](0, 255) < Ratburg_Gore_TrailChance * 32)
					a.Destroy();
			
			If(Target)
			{
				a.Target = Self.Target;
				a.Vel.X = (Target.Vel.X * 0.5) + FRandom[Blood](-4, 4);
				a.Vel.Y = (Target.Vel.Y * 0.5) + FRandom[Blood](-4, 4);
				a.Vel.Z = (Target.Vel.Z * 0.5) + FRandom[Blood](0, 8);
			}
			
			Else
				a.Destroy();
		}
		
		For(int i = 0; i != 40; i++)
		{
			Actor b = Spawn("RatburgerBloodParticles", Self.Pos);
			If(b)
			{
				If(Random[Blood](0, 255) < Ratburg_Gore_ParticleChance * 64)
						b.Destroy();
				
				If(Target)
				{
					b.Target = Self.Target;
					b.Vel.X = (Target.Vel.X * 0.5) + FRandom[Blood](-4, 4);
					b.Vel.Y = (Target.Vel.Y * 0.5) + FRandom[Blood](-4, 4);
					b.Vel.Z = (Target.Vel.Z * 0.5) + FRandom[Blood](0, 8);
				}
				
				Else
					b.Destroy();
			}
		}
	}
	
	States
	{
	Spawn:
		TNT1 AAAAA 0;
		B1_A ABCD 3;
		B1_A E 1
		{
			A_SetRoll(0,SPF_INTERPOLATE);
			A_ChangeVelocity(0,0,-1,CVF_RELATIVE);
			Scale.X *= 1.05; Scale.Y = Scale.X;
			A_FadeOut(0.1,1);
		}
		Wait;
	}
}

Class ChunkBlood : RatburgerBlood
{
	Default { Scale 0.25; +NODECAL }
	
	Override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		For(int i = 0; i != 8; i++)
		{
			Actor b = Spawn("RatburgerBloodParticles", Self.Pos);
			If(b)
			{
				If(Random[Blood](0, 255) < Ratburg_Gore_ParticleChance * 64)
						b.Destroy();
				
				If(Target)
				{
					b.Target = Self.Target;
					b.Vel.X = (Target.Vel.X * 0.5) + FRandom[Blood](-4, 4);
					b.Vel.Y = (Target.Vel.Y * 0.5) + FRandom[Blood](-4, 4);
					b.Vel.Z = (Target.Vel.Z * 0.5) + FRandom[Blood](0, 8);
				}
				
				Else
					b.Destroy();
			}
		}
	}
}

Class RatburgerBloodTrailer : Actor
{
	Default
	{
		PROJECTILE;
		-NOGRAVITY
		+DONTSPLASH
		+WINDTHRUST
		+RIPPER
		+BLOODLESSIMPACT
		+PAINLESS
		+THRUACTORS
		Gravity 0.75;
		Radius 1;
		Height 1;
		Decal "BloodSplat";
	}
	
	const BLOODY = SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION;
	
	Override int SpecialMissileHit(Actor victim)
	{ Return 1; }
	
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_SpawnItemEx("RatburgerBloodTrail",0,0,0,0,0,0,0,BLOODY|SXF_TRANSFERPOINTERS,0);
		Loop;
	Death:
		TNT1 A 0;
		TNT1 A 0
		{
			If(Random[Blood](0, 255) < 128) A_StartSound("BloodGround", CHAN_BODY, CHANF_OVERLAP, 0.5, 2.0);
			A_SpawnItemEx("RatburgerBloodPool",0,0,0,0,0,0,0,BLOODY|SXF_TRANSFERPOINTERS, Ratburg_Gore_PoolChance * 32);
		}
		B3_A ABCDE 5;
		Stop;
	}
}

Class RatburgerBloodTrail : RatburgerBloodTrailer
{
	Default
	{
		Gravity 0.05;
		RenderStyle "Translucent";
		Scale 0.67;
		Decal "None";
		-WINDTHRUST
		+FORCEDECAL
		+RANDOMIZE
		+ROLLSPRITE
		+ROLLCENTER
		+FORCEXYBILLBOARD
	} int timer;
	
	Override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		A_SetRoll(frandom(0,359));
		bSPRITEFLIP = random(0,1);
		Scale.X *= frandom(0.95,1.05);
		Scale.Y = Scale.X;
	}
	
	Override void Tick()
	{
		Super.Tick();
		If(IsFrozen()) Return;
		If(Timer++ >= 18) A_FadeOut(0.075, 1);
	}
	
	States
	{
	Spawn:
		B2_A ABCDE 5;
		Loop;
	Death:
		TNT1 A 5;
		Stop;
	}
}

Class RatburgerBloodParticles : RatburgerBloodTrail
{
	Default { Scale 0.25; Gravity 0.67; Alpha 0.75; }
	Override void PostBeginPlay() { Super.PostBeginPlay(); Timer = -17; }
	States
	{
	Spawn:
		B5_A B 1;
		Loop;
	Death:
		B3_A ABCDE 5 { Scale = (.33, .33); Invoker.Timer = 0; }
		Stop;
	}
}

Class RatburgerBloodPool : Actor
{
	Default
	{
		+FLATSPRITE
		+MOVEWITHSECTOR
		+NOBLOCKMAP
		+NOTIMEFREEZE
		RenderStyle "Translucent";
		Scale 0.0;
	}
	
	Override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		If(Self.Pos.Z <= FloorZ) 	{ bFLOORHUGGER = 1; bCEILINGHUGGER = 0; }
		If(Self.Pos.Z >= CeilingZ) 	{ bFLOORHUGGER = 0; bCEILINGHUGGER = 1; }
		
		If(!bFLOORHUGGER && !bCEILINGHUGGER)
		{
			Destroy();
			Return;
		}
		
		A_SetAngle(frandom(0,359));
		bSPRITEFLIP = random(0,1);
		Scale.X *= frandom(0.9,1.1);
		Scale.Y = Scale.X;
		A_FadeOut(frandom(0,0.2));
	} int timer;
	
	States
	{
	Spawn:
		B5_A AAAAAAAAAAAAAAA 1 { Scale.X += frandom(0.035, 0.055); Scale.Y = Scale.X; }
	Spawned:
		B5_A A 700 A_SetTics(140 * Ratburg_Gore_GibDuration);
		B5_A A 1 A_FadeOut(0.01, 1);
		Wait;
	}
}