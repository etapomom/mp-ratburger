Class ChosenPlayer : DoomPlayer
{
	Default
	{
		Health 100;
		Player.MaxHealth 100;
		Mass 100;
		Scale 0.475;
		Player.AttackZOffset 12;
		Player.FallingScreamSpeed 20, 48;
		Player.DisplayName "Caleb the Chosen One";
		Player.ColorRange 112, 127;
		//Player.DamageScreenColor "Red", 0.0;
		//Player.StartItem "TempWeapon";
		Player.StartItem "BloodhoundRevolver";
		Player.StartItem "SilverBullets", 54;
		Player.StartItem "Pitchfork";
	}
	
	Override void DeathThink ()
	{
		let player = self.player;
		int dir;
		double delta;

		player.Uncrouch();
		TickPSprites();

		player.onground = (pos.Z <= floorz);
		if (self is "PlayerChunk")
		{ // Flying bloody skull or flying ice chunk
			player.viewheight = 6;
			player.deltaviewheight = 0;
			if (player.onground)
			{
				if (Pitch > -19.)
				{
					double lookDelta = (-19. - Pitch) / 8;
					Pitch += lookDelta;
				}
			}
		}
		else If(!bIceCorpse)
		{
			If(!CountInv("DeathTicked")) { A_GiveInventory("DeathTicked"); Pitch = 0.; }
			/*player.deltaviewheight = 0;
			if (player.viewheight > 4) player.viewheight -= 2;
			if (player.viewheight < 4) player.viewheight = 	4;*/
			
			/*If(Pitch > -45) 		Pitch -= 1;
			if(abs(Pitch) > 45) 	Pitch = -45.;*/
			
			If(Player.ViewHeight > 2)
			{
				double viewheightdelta = (2. + player.viewheight) / 28;
				player.viewheight -= viewheightdelta;
			}
			
			if (Pitch > -45.)
			{
				double lookDelta = (-45. - Pitch) / 14;
				Pitch += lookDelta;
			}
		}
		player.mo.CalcHeight();
			
		if (player.attacker && player.attacker != self)
		{ // Watch killer
			double diff = deltaangle(angle, AngleTo(player.attacker));
			double delta = abs(diff);
	
			if (delta < 10)
			{ // Looking at killer, so fade damage and poison counters
				if (player.damagecount)
				{
					player.damagecount--;
				}
				if (player.poisoncount)
				{
					player.poisoncount--;
				}
			}
			delta /= 40.;
			Angle += clamp(diff, -4., 4.);
		}
		else
		{
			if (player.damagecount)
			{
				player.damagecount--;
			}
			if (player.poisoncount)
			{
				player.poisoncount--;
			}
		}		

		if ((player.cmd.buttons & BT_USE ||
			((deathmatch || alwaysapplydmflags) && sv_forcerespawn)) && !sv_norespawn)
		{
			if (Level.maptime >= player.respawn_time || ((player.cmd.buttons & BT_USE) && player.Bot == NULL))
			{
				player.cls = NULL;		// Force a new class if the player is using a random class
				player.playerstate = (multiplayer || level.AllowRespawn || sv_singleplayerrespawn || G_SkillPropertyInt(SKILLP_PlayerRespawn)) ? PST_REBORN : PST_ENTER;
				if (special1 > 2)
				{
					special1 = 0;
				}
			}
		}
	}
	
	/*Override void DeathThink ()
	{
		Super.DeathThink();
		If(!CountInv("DeathTicked")) { A_GiveInventory("DeathTicked"); Pitch = 0.; }
		If(!bIceCorpse)
		{
			player.deltaviewheight = 0;
			if (player.viewheight > 4) player.viewheight -= 1;
			if (player.viewheight < 4) player.viewheight = 	4;
			
			if (Pitch > -45) 		Pitch -= 3;
			if (abs(Pitch) < 45) 	Pitch = 45.;
		}
		player.mo.CalcHeight();
	}*/
	
	Override Void CheatGive(String name, int amount)
	{
		super.CheatGive(name, amount);
		if (name ~== "all" || name ~== "armor" || name ~== "everything")
		{
			CheatTake("Armor", 1);
			super.CheatGive("SuperArmorPickup", 1);
		}
	}
	
	Override Int DamageMObj(Actor Inflictor, Actor Source, Int Damage, Name MOD, Int Flags, Double Angle)
	{
		int hp1 = Player.Health;
		Super.DamageMObj(Inflictor, Source, Damage, MOD, Flags, Angle);
		int hp2 = Player.Health;
		
		//A_Log(String.Format("Damage: %d -- Prev. HP: %d -- Curr. HP: %d", Damage, hp1, hp2), True);
		
		If(hp2 < hp1 && MOD != 'EGO')
		{
			A_PlayerPain(hp1, hp2, mod);
			
			double al = Clamp(Damage * 0.01, 0.275, 1);
			int ti = Clamp(Damage * 1.5, 18, 175);
			color clr;
			
			clr = "Red";
			
			If(MOD == 'Octabrain') 		clr = "Purple";
			If(MOD == 'Shrinker')		clr = "Green";
			If(MOD == 'Corrosive')		clr = "Green";
			If(MOD == 'Slime')			clr = "Green";
			If(MOD == 'Poison')			clr = "Green";
			If(MOD == 'Fire')			clr = "Orange";
			IF(MOD == 'Lava')			clr = "Orange";
			If(MOD == 'Shock')			clr = "White";
			If(MOD == 'Ice')			clr = "Blue";
			If(MOD == 'Cold')			clr = "Blue";
			If(MOD == 'Drowning')		clr = "Black";
			
			If(hp2 <= 0) { al = 1; ti = 350; }
			
			//A_SetBlend(clr, al, ti);
		}
		
		Return Damage;
	}
	
	Void A_PlayerPain(int hp1, int hp2, name type)
	{
		/*If(hp1 <= 0 || hp2 <= 0) 
			Return;*/
		
		int dmg = hp1 - hp2;
		sound ouch;
		
		Ouch = "Ouch";
		
		A_StopSound(CHAN_VOICE);
		
		If(type == 'Drowning') Ouch = "Drown";
		If(type == 'Choke') Ouch = "Choke";
		If(type == 'Falling') Ouch = "Ouch";
		
		If(hp1 <= 0 || hp2 <= 0) 
		{
			If(Type == 'Drowning' || Type == 'Choke') Ouch = "Choked";
			Else Ouch = "Death";
		}
		
		A_StartSound(Ouch, CHAN_VOICE);
	}
	
	bool beastready;
	Override Void Tick()
	{	
		Super.Tick();
		
		If(!Player || !Player.mo || Player.mo != Self) 
		{ Return; }
		
		If(Health>0 && countinv("deathticked")) 
		{
			Pitch = 0.;
			A_SetInventory("DeathTicked", 0);
		}
		
		If(InStateSequence(CurState, ResolveState("Spawn")) && Player.Health > 0)
				A_GiveInventory("CalebIdleTimer", 1);
		
		Else A_SetInventory("CalebIdleTimer",0);
		
		If(Level.Time % 7 == 0 && Player.Health > 0)
		{
			ThinkerIterator Baddies = ThinkerIterator.Create();
			Actor mo;
			While(mo = Actor(Baddies.Next()))
			{
				If(!mo || mo.Health <= 0 || !mo.bISMONSTER || !mo.bSHOOTABLE || mo.Distance2D(self) > 1024 || !mo.CheckSight(Self,SF_IGNOREVISIBILITY))
					Continue;
				
				If(mo && mo.Health > 0 && mo.Distance2D(Self) < 4096 && mo.bISMONSTER && mo.bBOSS && mo.CheckSight(Self,SF_IGNOREVISIBILITY) && mo.Target && !mo.CountInv("CalebBossChecked"))
				{
					mo.A_GiveInventory("CalebBossChecked",1);
					A_GiveInventory("CalebBossTalk",1);
				}
			}
		}
		
		If(!BeastReady && CountInv("BeastMeter") >= 5000) 
		{ 
			BeastReady = True; 
			A_StartSound("Ping", CHAN_AUTO, CHANF_OVERLAP|CHANF_LOCAL); 
			A_SetBlend("DarkRed", 0.75, 24);
			A_StartSound("BeastReady", CHAN_AUTO, CHANF_OVERLAP|CHANF_LOCAL);
			A_QuakeEx(1, 1, 1, 18, 0, 8, "");
			//A_GiveInventory("BloodbathClaws");
			//A_SelectWeapon("BloodbathClaws");
		}
		
		If(CountInv("BeastMeter") < 5000) BeastReady = False;
	}
	
	States
	{
	Spawn:
		CB01 A -1;
		Loop;
	See:
		CB01 BCDEFG 3;
		Loop;
	Melee:
		CB01 I 2 BRIGHT;
	Missile:
		CB01 A 2;
		Goto Spawn;
	Pain:
		CB01 H 4;
		Goto Spawn;
	XDeath:
	Death:
		CB01 JKL 5;
		CB01 M 5 A_NoBlocking();
		CB01 NO 5;
		CB01 P -1;
		Stop;
	}
}

Class BeastMeter : Inventory { Default { Inventory.MaxAmount 5000; } }

Class CalebTalkTimer : Inventory { Default { Inventory.MaxAmount 350; } }
Class CalebTalkQueue : Inventory { Default { Inventory.MaxAmount 35; } }
Class CalebMonsterCounter : Inventory { Default { Inventory.MaxAmount 100; } }
Class CalebKillTalk : Inventory { Default { Inventory.MaxAmount 1; } }
Class CalebGibTalk : Inventory { Default { Inventory.MaxAmount 1; } }
Class CalebMonsterTalk : Inventory { Default { Inventory.MaxAmount 1; } }
Class CalebBossTalk : Inventory { Default { Inventory.MaxAmount 1; } }
Class CalebBossKillTalk : Inventory { Default { Inventory.MaxAmount 1; } }
Class CalebBrainTalk : Inventory { Default { Inventory.MaxAmount 1; } }
Class CalebBossChecked : Inventory { Default { Inventory.MaxAmount 1; } }
Class CalebStartTalk : Inventory { Default { Inventory.MaxAmount 1; } }
Class CalebStartTalked : Inventory { Default { Inventory.MaxAmount 1; } }
Class CalebIdleTimer : Inventory { Default { Inventory.MaxAmount 350; } }
Class CalebStarted : Inventory { Default { Inventory.MaxAmount 1; } }
Class DeathTicked : Inventory { Default { Inventory.MaxAmount 1; } }

//RIP ACS LMAO
Class CalebTalkHandler : Inventory
{
	Default { Inventory.MaxAmount 1; }
	
	bool init;
	Override Void DoEffect()
	{
		Super.DoEffect();
		Let p = Owner;
		
		If(!Init)
		{
			p.A_SetInventory("CalebTalkTimer",0); 
			p.A_SetInventory("CalebMonsterCounter",0);
			p.A_SetInventory("CalebMonsterTalk",0);
			p.A_SetInventory("CalebBossTalk",0);
			p.A_SetInventory("CalebBrainTalk",0);
			p.A_SetInventory("CalebKillTalk",0);
			p.A_SetInventory("CalebGibTalk",0);
			p.A_SetInventory("CalebIdleTimer",0);
			If(!p.CountInv("CalebStartTalked")) { p.A_GiveInventory("CalebStartTalk"); }
			Init = True;
		}
		
		If(Init && GetCVar("Blood_CalebTalk"))
		{
			If(p.CountInv("CalebStartTalk") && !p.CountInv("CalebStartTalked") && GetCVar("Blood_StartTalk"))
			{
				p.A_PlaySound("Start",CHAN_VOICE);
				p.A_GiveInventory("CalebStartTalked",1);
			}
			
			If(p.CountInv("CalebKillTalk") && !p.CountInv("CalebTalkTimer") && GetCVar("Blood_KillTalk"))
			{
				p.A_PlaySound("KillTalk",CHAN_VOICE);
				p.A_SetInventory("CalebTalkTimer",105);
			}
			
			If(p.CountInv("CalebGibTalk") && !p.CountInv("CalebTalkTimer") && GetCVar("Blood_GibTalk") && GetCVar("Blood_KillTalk"))
			{
				p.A_PlaySound("GibTalk",CHAN_VOICE);
				p.A_SetInventory("CalebTalkTimer",105);
			}
			
			If(p.CountInv("CalebBossTalk") && !p.CountInv("CalebTalkTimer") && GetCVar("Blood_BossTalk"))
			{
				p.A_PlaySound("SeeBadass",CHAN_VOICE);
				p.A_SetInventory("CalebTalkTimer",350);
			}
			
			If(p.CountInv("CalebBossKillTalk") && GetCVar("Blood_KillTalk"))
			{
				p.A_PlaySound("BadassKill",CHAN_VOICE);
				p.A_SetInventory("CalebTalkTimer",350);
			}
			
			If(p.CountInv("CalebMonsterTalk") && !p.CountInv("CalebTalkTimer") && GetCVar("Blood_MonsterTalk"))
			{
				p.A_PlaySound("TooMany",CHAN_VOICE);
				p.A_SetInventory("CalebTalkTimer",350);
			}
			
			If(p.CountInv("CalebBrainTalk") && !p.CountInv("CalebTalkTimer") && GetCVar("Blood_BossTalk"))
			{
				p.A_PlaySound("SeeBrain",CHAN_VOICE);
				p.A_SetInventory("CalebTalkTimer",350);
			}
			
			If(p.CountInv("CalebIdleTimer") >= 350 && !p.CountInv("CalebTalkTimer") && GetCVar("Blood_IdleTalk"))
			{
				If(Random[Talk](1, 10) >= 8 && GetCVar("Blood_IdleSong")) p.A_PlaySound("IdleSong",CHAN_VOICE);
				Else p.A_PlaySound("Idle",CHAN_VOICE);
				p.A_SetInventory("CalebTalkTimer",350);
			}
			
			If(p.CountInv("CalebTalkTimer") != 0)
			{
				p.A_SetInventory("CalebMonsterTalk",0);
				p.A_SetInventory("CalebBossTalk",0);
				p.A_SetInventory("CalebBossKillTalk",0);
				p.A_SetInventory("CalebBrainTalk",0);
				p.A_SetInventory("CalebKillTalk",0);
				p.A_SetInventory("CalebGibTalk",0);
				p.A_SetInventory("CalebStartTalk",0);
				p.A_SetInventory("CalebIdleTimer",0);
			}
			
			If(p.CountInv("CalebTalkTimer")) { p.A_TakeInventory("CalebTalkTimer",1); }
		}
	}
}

// Player specific gore
Class PlayerLegGibL : RatburgerBloodChunkBase
{
	Default
	{
		Scale 0.75;
		-BOUNCEONFLOORS
		-BOUNCEONCEILINGS
		-BOUNCEONWALLS
		-CANBOUNCEWATER
		-USEBOUNCESTATE
	}
	States
	{
	Spawn:
		C7_A A 0;
		Goto Super::Flight;
	Death:
		TNT1 A 5
		{
			A_SpawnItemEx("RatburgerBloodPool",0,0,0,0,0,0,0,BLOODY|SXF_TRANSFERPOINTERS, Ratburg_Gore_PoolChance * 32);
			For(int i = 0; i != 4; i++)
			{
				Double VX = Invoker.Vel.X * 0.125;
				Double VY = Invoker.Vel.Y * 0.125;
				A_SpawnItemEx("RatburgerBloodChunk1b",0,0,Invoker.Height * 0.5,vx+frandom(-6,6),vy+frandom(-6,6),(invoker.vel.z*0.125)+frandom(4,12),frandom(0,359),BLOODY|SXF_TRANSFERPOINTERS,0);
			}
			Return ResolveState("Null");
		}
		Stop;
	}
}

Class PlayerLegGibR : PlayerLegGibL { Default { +SPRITEFLIP } }

Class PlayerHeadGib : Actor
{
	Default
	{
		Radius 16;
		Height 16;
		Speed 0;
		Scale 0.55;
		Gravity 0.75;
		Health 100;
		Mass 65536;
		DamageFunction (0);
		PROJECTILE;
		+SOLID
		+SHOOTABLE
		+RANDOMIZE
		-NOGRAVITY
		+BOUNCEONWALLS	
		+BOUNCEONFLOORS	
		+BOUNCEONCEILINGS
		+ALLOWBOUNCEONACTORS	
		+BOUNCEONACTORS
		+CANBOUNCEWATER	
		+LOOKALLAROUND
		+CANBOUNCEWATER
		+USEBOUNCESTATE
		+FLOORCLIP
		+THRUSPECIES
		Species "Zombie";
		BounceCount 350;
		BounceFactor 0.5;
		WallBounceFactor 0.5;
		Tag "Zombie Head";
	} int Bounces, Bounces2, Bounced;
	
	Override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		A_SpawnItemEx("RatburgerCorpseHitbox",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION|SXF_ORIGINATOR|SXF_SETMASTER|SXF_ISTRACER,0);
	}
	
	Override void Tick()
	{
		Super.Tick();
		
		If(IsFrozen()) Return;
		
		If(Self.Pos.Z > Self.FloorZ) 
		{ 
			A_SpawnItemEx("RatburgerBloodTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION,0);
		}
		
		If(Bounces2 >= 32 || bCORPSE == 1)
		{
			A_PlaySound("Gibbed",CHAN_BODY);
			SpawnBlood(Self.Pos+(frandom(-8,8),frandom(-8,8),frandom(-8,8)),frandom(0,359),15);
			SpawnBlood(Self.Pos+(frandom(-8,8),frandom(-8,8),frandom(-8,8)),frandom(0,359),15);
			SpawnBlood(Self.Pos+(frandom(-8,8),frandom(-8,8),frandom(-8,8)),frandom(0,359),15);
			SpawnBlood(Self.Pos+(frandom(-8,8),frandom(-8,8),frandom(-8,8)),frandom(0,359),15);
			SetStateLabel("Null");
		}
		Bounced--;
	}
	
	Override bool CanCollideWith(Actor Other, Bool Passive)
	{
		If(Passive)
		{
			If(!Other)
				Return False;
			
			If(Other && Other is 'RatburgerCorpseHitbox')
				Return False;
		}
		
		If(!Passive)
		{
			If(!Other || !bSPECIALFIREDAMAGE || Bounced > 0)
			{ Return False; }
			
			Else
			{
				If(!Other.Player || Other is 'RatburgerCorpseHitbox') { Return False; }
				If(!bSPECIALFIREDAMAGE) { Return False; }
				
				Double NewVelX = BloodyHandler.GetRelativeX(Other,-4);
				Double NewVelY = BloodyHandler.GetRelativeY(Other,-4);
				Double NewVelZ = Max(NewVelX, NewVelY) * 0.25;
				Double NewVelXY = Max(NewVelX, NewVelY);
				
				NewVelXY = Clamp(NewVelXY, 16, 32);
				NewVelZ  = Clamp(NewVelZ,  8, 24);
				
				If(Tracer)
				{
					//AngleTo(Other);
					//A_ChangeVelocity(NewVelXY, 0, NewVelZ, CVF_RELATIVE, 0);
					Tracer.AngleTo(Other);
					Tracer.A_ChangeVelocity(NewVelXY, 0, NewVelZ, CVF_RELATIVE, 0);
					A_SetAngle(Angle - 180);
					Tracer.Angle = Angle;
					If(abs(Tracer.Vel.X+Tracer.Vel.Y+Tracer.Vel.Z) > 64) { Bounces2 = 32; }
				}
				
				If(Self.Pos.Z >= Other.Height) { Vel.Z *= -1; }
				
				A_PlaySound("Kick",CHAN_BODY);
				Bounces = 0;
				Bounces2++;
				Bounced = 10;
				bSPECIALFIREDAMAGE = 0;
				Return True;
			}
		}
		Return True;
	}		
	
	States
	{
	Spawn:
		C8_A ABCDEFGH 4
		{
			If((Invoker.Bounces >= 4 && Self.Pos.Z <= Self.FloorZ + 8) || Abs(Vel.Z) == 0) 
			{
				Vel.Z = 0; 
				Return ResolveState("Grounded"); 
			}
			Return ResolveState(Null);
		}
		Loop;
	Bounce:
		TNT1 A 0
		{
			Invoker.Bounces++;
			If((Invoker.Bounces >= 4 && Self.Pos.Z <= Self.FloorZ + 8) || Abs(Vel.Z) == 0) 
			{
				Vel.Z = 0; 
				Return ResolveState("Grounded"); 
			}
			Return ResolveState(Null);
		}
		Goto Spawn;
	Grounded:
		C8_A A 1 { bSPECIALFIREDAMAGE = 1; Vel.X *= 0.9; Vel.Y *= 0.9; }
		C8_A A 0 A_CheckFloor("Grounded");
		Goto Spawn;
	Death:
		TNT1 AAA 0 SpawnBlood(Self.Pos,frandom(0,359),15);
		Stop;
	}
}

Class PlayerHatGib : RatburgerBloodChunkBaseFlippable
{
	Default
	{
		Gravity 0.75;
		Scale 1.15;
		WallBounceFactor 1.0;
		BounceFactor 0.1;
		-USEBOUNCESTATE
	}
	
	Override void Tick()
	{
		Actor.Tick();
		//If(Abs(Vel.Length()) > 16) { boom = 1; }
		
		If(bCORPSE == 1)
		{
			//If(Random[Blood](0, 255) < 32) A_StartSound("Gibbed", CHAN_BODY, CHANF_OVERLAP, 0.15, 2.0);
			SetStateLabel("Null");
		}
		
		prevvel.x = vel.x;
		prevvel.y = vel.y;
		
		If(Self.Pos.Z <= FloorZ) Vel *= 0.99;
	}
	
	States
	{
	Spawn:
		C6_A ABCD 0 A_Jump(2, "Flight");
		Loop;
	Death:
		"####" E 0 { Vel = (prevvel.x, prevvel.y, 0); }
		"####" E 700 
		{ 
			A_SetTics(105 * Ratburg_Gore_GibDuration);
			
			bROLLSPRITE = 0; 
			bNOTIMEFREEZE = 1; 
		}
		"####" E 1 A_FadeOut(0.01,1);
		Wait;
	}
}

Class PlayerHandGibL : RatburgerBloodChunkBase //Hand
{
	Default { Scale 0.85; BounceFactor 0.25; }
	
	Override void Tick()
	{
		Actor.Tick();
		//If(Abs(Vel.Length()) > 16) { boom = 1; }
		
		If(bCORPSE == 1)
		{
			//If(Random[Blood](0, 255) < 32) A_StartSound("Gibbed", CHAN_BODY, CHANF_OVERLAP, 0.15, 2.0);
			SetStateLabel("Null");
		}
		
		prevvel.x = vel.x;
		prevvel.y = vel.y;
		
		If(Self.Pos.Z <= FloorZ) Vel *= 0.99;
	}
	
	States
	{
	Spawn:
		C1_A A 0;
		Goto Super::Flight;
	}
}
Class PlayerHandGibR : PlayerHandGibL { Default { +SPRITEFLIP } }