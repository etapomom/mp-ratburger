Class BloodbathCorpseHitbox : Actor
{
	Default
	{
		+SHOOTABLE
		+SOLID
		+CANPASS
		+THRUSPECIES
		+SLIDESONWALLS
		Radius 1;
		Height 1;
		Health 1;
		Gravity 0.5;
		Species "BloodbathCorpseHitbox";
		Tag "Carcass";
	}
	
	Bool Chunk, Ready; Int cnt;
	
	Override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		If(!Master) { Destroy(); Return; }
		
		//Ready = True;
		
		If(Master is 'BloodbathChunk1')
		{	Chunk = True; Ready = True;		}
		
		A_SetSize(Master.Radius, Master.Height); 
		
		CVar CarcassHealth = CVar.FindCVar("BB_CarcassHP");
		Health = Master.GetSpawnHealth() * CarcassHealth.GetFloat();
		Mass = Master.Mass * 0.5;
		bNOBLOOD = Master.Default.bNOBLOOD;
	}
	
	Override void Tick()
	{
		Super.Tick();
		
		If(!Master) { Destroy(); Return; }
		
		If(Master && Master.bISMONSTER && Master.Health > 0) 
		{ 
			Destroy(); 
			Return; 
		}
		
		CVar GibBoss = CVar.FindCVar("BB_GibBosses");
		CVar GibThis = CVar.FindCVar("BB_GibCorpses");
		
		If(Ready == False || (Master.bBOSS && GibBoss.GetBool() == 0) || bNOBLOOD || GibThis.GetBool() == 0)
		{
			CVar CarcassHealth = CVar.FindCVar("BB_CarcassHP");
			Health = Master.GetSpawnHealth() * CarcassHealth.GetFloat();
		}
		
		If(Master.Vel ~== (0,0,0) && Ready == False && cnt++ >= 18 && Master.Tics <= -1)
		{
			Ready = True;
			If(!Chunk)
				A_SetSize(Master.Radius, Master.DeathHeight);
		}
		
		If(GibThis.GetBool() == 0)
			bSHOOTABLE = 0;
		
		Else
			bSHOOTABLE = 1;
		
		If(Health > 0) Master.SetOrigin(Self.Pos,1); 
	}
	
	Override void Die(Actor source, Actor inflictor, int DmgFlags, Name MeansOfDeath)
	{
		Super.Die(Source, Inflictor, DmgFlags, MeansOfDeath);
		
		If(Chunk == True)
		{
			CVar TrailChance = Cvar.FindCVar("BB_TrailChance");
			
			Actor b = Master.Spawn("BloodbathTrailer", Self.Pos);
			If(b)
			{
				If(Random[Blood](0, 255) < TrailChance.GetInt() * 32)
					b.Destroy();
				
				b.Vel.X = (Self.Vel.X * 0.1) + FRandom[Blood](-6, 6);
				b.Vel.Y = (Self.Vel.Y * 0.1) + FRandom[Blood](-6, 6);
				b.Vel.Z = (Self.Vel.Z * 0.1) + FRandom[Blood](0, 12);
				BloodbathHandler.TranslateBlood(Master, b);
			}
			
			For(int i = 0; i != 4; i++)
			{
				Actor a = Master.Spawn("BloodbathChunk1b", Master.Pos);
				If(a)
				{
					If(Random[Blood](0, 255) < TrailChance.GetInt() * 32)
						a.Destroy();
					
					BloodbathHandler.TranslateBlood(Master, a);
					a.Vel.X = (Self.Vel.X * 0.1) + FRandom[Blood](-6, 6);
					a.Vel.Y = (Self.Vel.Y * 0.1) + FRandom[Blood](-6, 6);
					a.Vel.Z = (Self.Vel.Z * 0.1) + FRandom[Blood](4, 12);
				}
			}
			
			Master.bCORPSE = 1;
		}
		
		Else
		{
			BloodbathHandler.SpawnGibs(Master, True);
			//Master.Destroy();
		}
	}
	
	Override int DamageMObj(Actor inflictor, Actor source, int Damage, Name mod, int Flags, double Angle)
	{
		CVar GibBoss = CVar.FindCVar("BB_GibBosses");
		CVar GibThis = CVar.FindCVar("BB_GibCorpses");
		If((Master && Master.bBOSS && GibBoss.GetBool() == 0) || bNOBLOOD || (!Chunk && GibThis.GetBool() == 0) || !Ready)
			Return Super.DamageMObj(Inflictor, Source, 0, Mod, Flags, Angle);
		
		Return Super.DamageMObj(Inflictor, Source, Damage, MOD, Flags, Angle);
	}
	
	Override bool CanCollideWith(Actor Other, Bool Passive)
	{
		CVar GibThis = CVar.FindCVar("BB_GibCorpses");
		If(GibThis.GetBool() == 0 || Chunk)
			Return False;
		
		If(Passive)
		{
			If(!Other)
				Return False;
				
			If(Other && Other == Master)
				Return False;
			
			If(Other && Other.Player)
				Return False;
			
			If(Other && Other.bISMONSTER)
				Return False;
			
			If(Other && !Other.bMISSILE)
				Return False;
			
			If(Other && Other is 'BloodbathCorpseHitbox')
				Return False;
				
			If(Other && Other.GetSpecies() == 'BloodbathCorpseHitbox')
				Return False;
			
			If(Other && Other != Master && Other.bMISSILE)
				Return True;
		}
		
		If(!Passive)
		{
			If(!Other)
				Return False;
				
			If(Other && Other == Master)
				Return False;
			
			If(Other && Other.Player)
				Return False;
			
			If(Other && Other.bISMONSTER)
				Return False;
			
			If(Other && Other is 'BloodbathCorpseHitbox')
				Return False;
				
			If(Other && Other.GetSpecies() == 'BloodbathCorpseHitbox')
				Return False;
		}
		
		Return True;
	}
	
	States
	{
	Spawn:
		TNT1 A 1;
		Loop;
	Death:
		TNT1 A 1;
		TNT1 A 35 { Master.SetOrigin((32768,32768,32768), 0); Master.bINVISIBLE = True; }
		TNT1 A 35 { Master.Destroy(); }
		Stop;
	}
}